plugins {
    id("java-library")
    id("maven-publish")
    id("idea")
    id("net.neoforged.moddev") version "2.0.88"
    kotlin("jvm") version "2.1.20"
}

val mod_version: String by project
val mod_group_id: String by project
val mod_id: String by project

version = mod_version
group = mod_group_id

repositories {
    mavenLocal()
    maven {
        name = "Kotlin for Forge"
        url = uri("https://thedarkcolour.github.io/KotlinForForge/")
        content { includeGroup("thedarkcolour") }
    }
    maven {
        name = "Create"
        url = uri("https://maven.createmod.net")
    }
    maven {
        name = "Registrate"
        url = uri("https://mvn.devos.one/snapshots")
    }
    maven {
        name = "ForgeConfigAPIPort"
        url = uri("https://raw.githubusercontent.com/Fuzss/modresources/main/maven")
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = uri("https://maven.blamejared.com/")
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = uri("https://modmaven.dev")
    }
}

base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
kotlin.jvmToolchain(21)

val neo_version: String by project
val parchment_mappings_version: String by project
val parchment_minecraft_version: String by project

neoForge {
    // Specify the version of NeoForge to use.
    version = neo_version

    parchment {
        mappingsVersion = parchment_mappings_version
        minecraftVersion = parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    // accessTransformers.add('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        create("client") {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            systemProperty("neoforge.enabledGameTestNamespaces", mod_id)
        }

        create("server") {
            server()
            programArgument("--nogui")
            systemProperty("neoforge.enabledGameTestNamespaces", mod_id)
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        create("gameTestServer") {
            type = "gameTestServer"
            systemProperty("neoforge.enabledGameTestNamespaces", mod_id)
        }

        create("data") {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll(
                listOf(
                    "--mod",
                    mod_id,
                    "--all",
                    "--output",
                    file("src/generated/resources/").absolutePath,
                    "--existing",
                    file("src/main/resources/").absolutePath
                )
            )
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty("forge.logging.markers", "REGISTRIES")

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        create(mod_id) {
            sourceSet(sourceSets["main"])
        }
    }
}

// Include resources generated by data generators.
sourceSets["main"].resources { srcDir("src/generated/resources") }

val minecraft_version: String by project

dependencies {
    fun externalImplementation(dependency: String, strictly: String, prefer: String) {
        jarJar(implementation(dependency) {
            version {
                // The version range your mod is actually compatible with.
                // Note that you may receive a *lower* version than your preferred if another
                // Mod is only compatible up to 1.7.24, for example, your mod might get 1.7.24 at runtime.
                strictly(strictly)
                prefer(prefer) // The version actually used in your dev workspace
            }
        })
        "additionalRuntimeClasspath"("$dependency:$prefer")
    }

    // kff
    implementation("thedarkcolour:kfflang-neoforge:5.8.0")
    implementation("thedarkcolour:kfflib-neoforge:5.8.0")
    implementation("thedarkcolour:kffmod-neoforge:5.8.0")
    "additionalRuntimeClasspath"("org.jetbrains.kotlin:kotlin-stdlib")
    "additionalRuntimeClasspath"("org.jetbrains.kotlin:kotlin-reflect")
    //"additionalRuntimeClasspath"("org.jetbrains.kotlinx:kotlinx-coroutines-core")
    //"additionalRuntimeClasspath"("org.jetbrains.kotlinx:kotlinx-serialization-core")
    //"additionalRuntimeClasspath"("org.jetbrains.kotlinx:kotlinx-serialization-json")

    // create
    implementation("com.simibubi.create:create-${minecraft_version}:6.0.4-73:slim") { isTransitive = false }
    implementation("net.createmod.ponder:Ponder-NeoForge-${minecraft_version}:1.0.46")
    compileOnly("dev.engine-room.flywheel:flywheel-neoforge-api-${minecraft_version}:1.0.2")
    runtimeOnly("dev.engine-room.flywheel:flywheel-neoforge-${minecraft_version}:1.0.2")
    implementation("com.tterrag.registrate:Registrate:MC1.21-1.3.0+62")

    // Example mod dependency with JEI
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    val jei_version = "19.8.2.99"
    compileOnly("mezz.jei:jei-1.21-neoforge-api:${jei_version}")
    runtimeOnly("mezz.jei:jei-1.21-neoforge:${jei_version}")

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // implementation "blank:coolmod-${mc_version}:${coolmod_version}"

    // Example mod dependency using a file as dependency
    // implementation files("libs/coolmod-${mc_version}-${coolmod_version}.jar")

    // Example project dependency using a sister or child project:
    // implementation project(":myproject")

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
}

val minecraft_version_range: String by project
val neo_version_range: String by project
val loader_version_range: String by project
val mod_name: String by project
val mod_license: String by project
val mod_authors: String by project
val mod_description: String by project

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
val generateModMetadata = tasks.register<ProcessResources>("generateModMetadata") {
    val replaceProperties = mapOf(
        "minecraft_version" to minecraft_version,
        "minecraft_version_range" to minecraft_version_range,
        "neo_version" to neo_version,
        "neo_version_range" to neo_version_range,
        "loader_version_range" to loader_version_range,
        "mod_id" to mod_id,
        "mod_name" to mod_name,
        "mod_license" to mod_license,
        "mod_version" to mod_version,
        "mod_authors" to mod_authors,
        "mod_description" to mod_description
    )
    inputs.properties(replaceProperties)
    expand(replaceProperties)
    from("src/main/templates")
    into("build/generated/sources/modMetadata")
}

// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets["main"].resources.srcDir(generateModMetadata)
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask(generateModMetadata)

// Example configuration to allow publishing using the maven-publish plugin
publishing {
    publications {
        register<MavenPublication>("mavenJava") {
            from(components["java"])
        }
    }
    repositories {
        maven {
            url = uri("file://${project.projectDir}/repo")
        }
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        isDownloadSources = true
        isDownloadJavadoc = true
    }
}
